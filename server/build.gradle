import java.util.regex.Matcher

buildscript {
  repositories {
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath 'com.netflix.nebula:gradle-ospackage-plugin:6.1.1'
  }
}

apply plugin: 'java-library'
apply plugin: 'application'
apply plugin: 'nebula.ospackage-application'

apply plugin: 'idea'
idea.module.outputDir file("out/production/classes")

group = 'ai.swim'
description = 'Real-time Traffic App'
version = project.property('application.version')
mainClassName = 'swim.traffic.TrafficPlane'
ext.moduleName = 'swim.traffic'
ext.compilerArgs = ['-Xlint:all']

// Build with `gradle -Pno-modules` to force compatibility with legacy JVMs.
def javaVersion = System.getProperty('java.version').split('\\.')
ext.jvmVersion = (javaVersion[0] as Integer) > 1 ? javaVersion[0] as Integer : javaVersion[1] as Integer
ext.useModules = jvmVersion >= 9 && !hasProperty('no-modules')

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
  mavenCentral()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
  api group: 'ai.swim', name: 'swim-api', version: version
  implementation group: 'ai.swim', name: 'swim-server', version: version
}

afterEvaluate {
  configurations {
    apiElements {
      sourceCompatibility = JavaVersion.VERSION_1_8
      targetCompatibility = JavaVersion.VERSION_1_8
    }
  }

  if (useModules) {
    task compileModuleInfoJava(type: JavaCompile) {
      dependsOn 'compileJava'
      doFirst {
        options.compilerArgs = [
          '--module-path', compileJava.classpath.asPath,
        ]
      }
      options.compilerArgs += compilerArgs
      options.encoding = 'UTF-8'
      classpath = files()
      source = sourceSets.main.allJava
      sourceCompatibility = JavaVersion.VERSION_1_9
      targetCompatibility = JavaVersion.VERSION_1_9
      destinationDir = compileJava.destinationDir
      include '*module-info*'
    }
    classes.dependsOn compileModuleInfoJava
    def dependents = configurations.api.dependencies + configurations.implementation.dependencies
    def dependentProjects = dependents.findAll { it instanceof ProjectDependency }
    dependentProjects.each { dependency ->
      compileModuleInfoJava.dependsOn ":${dependency.name}:compileModuleInfoJava"
    }
  }

  compileJava {
    options.compilerArgs += compilerArgs
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    exclude '*module-info*'
  }

  jar {
    inputs.property('moduleName', moduleName)
    manifest {
      attributes(
        'Implementation-Title': moduleName,
        'Implementation-Version': version,
        'Main-Class': mainClassName)
    }
  }

  task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives javadocJar, sourcesJar
  }

  run {
    dependsOn jar
    doFirst {
      jvmArgs += [
        '--module-path', files(configurations.runtimeClasspath, jar.archivePath).asPath,
        '--add-modules', 'swim.store.db,swim.reflect',
        '--module', "${moduleName}/${mainClassName}",
      ]
      classpath = files()
    }
  }

  startScripts {
    inputs.property("moduleName", moduleName)
    doFirst {
      classpath = files()
      defaultJvmOpts = [
        '--module-path', 'APP_HOME_LIBS',
        '--add-modules', 'swim.store.db,swim.reflect',
        '--module', "${moduleName}/${mainClassName}",
      ]
    }
    doLast {
      def bashFile = new File(outputDir, applicationName)
      String bashContent = bashFile.text
      bashFile.text = bashContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('$APP_HOME/lib'))

      def batFile = new File(outputDir, applicationName + ".bat")
      String batContent = batFile.text
      batFile.text = batContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('%APP_HOME%\\lib'))
    }
  }

  ospackage {
    release '1'
    prefix '/opt/swim-traffic'
  }

  task packageDeb(type: Deb) {
    maintainer = 'chris@swim.ai'

    configurationFile("/etc/sysconfig/${project.name}")
    preInstall "addgroup --quiet --system ${project.name}"
    preInstall "adduser --quiet --system --ingroup ${project.name} --no-create-home --disabled-password ${project.name}"
    postInstall "systemctl preset ${project.name} > /dev/null 2>&1"
    postInstall "systemctl start ${project.name} > /dev/null 2>&1"
    preUninstall  "systemctl disable ${project.name} > /dev/null 2>&1"
    preUninstall  "systemctl stop ${project.name} > /dev/null 2>&1"
    postUninstall "systemctl daemon-reload > /dev/null 2>&1"

    from('pkg') {
      into '/etc/systemd/system'
      include '*.service'
      addParentDirs false
      expand project.properties
      user 'root'
      permissionGroup 'root'
      fileMode = 0644
    }

    from('pkg') {
      into '/etc/sysconfig'
      include "${project.name}"
      user 'root'
      permissionGroup 'root'
      fileMode = 0644
      fileType CONFIG | NOREPLACE
    }
  }
}
